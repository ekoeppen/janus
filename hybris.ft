hex

: binary        $2 base ! ;

include intel-hex.ft

wordlist constant target-wordlist
wordlist constant meta-wordlist
  $20000 constant target#
       4 constant tcell

variable tdp           0 tdp !
variable tvp   $20000000 tvp !
variable tlast         0 tlast !

create #target target# allot
       #target target# -1 fill

1 constant branch-range-exceeded
2 constant call-range-exceeded

: pack$     dup >r 2dup c! 1+ 2dup + 0 swap ! swap cmove r> ;

: there     tdp @ ;
: torg      tdp ! ;
: taligned  tcell 1- + tcell negate and ;
: talign    there taligned tdp ! ;
: tallot    tdp +! ;
: t!        #target + l! ;
: tc@       #target + c@ ;
: t@        #target + l@ ;
: tc!       #target + c! ;
: th!       #target + w! ;
: t,        there t! tcell tallot ;
: tc,       there tc! 1 tallot ;
: th,       there th! 2 tallot ;

: t'        bl parse target-wordlist search-wordlist 0= throw >body @ ;
: tfind     target-wordlist search-wordlist 0= throw >body @ ;
: mcreate   get-current target-wordlist set-current create set-current ;

: tffa      tcell + ;
: tnfa      tcell + 1 + ;
: tcfa      tcell + 1 + dup c@ + taligned ;

: ,enter    $b500 th, ;
: ,exit     $bd00 th, ;

: ,call     4 - there -
            dup -$1000000 $1000000 within 0= if call-range-exceeded throw then
            1 #23 lshift 1- and
            dup #12 rshift %0000011111111111 and $F000 or th,
                 #1 rshift %0000011111111111 and $F800 or th, ;
: ,inline-branch
            4 - dup
            -$1000 $1009 within 0= if branch-range-exceeded throw then
            2/ %0000011111111111 and %1110000000000000 or th, ;
: ,inline-?branch
            8 - dup
            -$200 $200 within 0= if branch-range-exceeded throw then
            $0001 th, $ce01 th,
            2/ %0000000011111111 and %1101000000000000 or th, ;

: thead     ( b u -- : compile word header into the target image )
            talign
            there tlast @ t, tlast !
            $ff7f th,
            there #target + pack$ c@ 1+ tallot talign ;

: lookahead ( -- b u : parse a word, but leave it in the input stream )
            >in @ >r bl parse r> >in ! ;

: h:        ( -- : create a word with no name in the target dictionary )
            mcreate there , does> @ ,call ;

: t:        ( "name", -- : creates a word in the target dictionary )
            lookahead thead h: ;
: t;        ;

get-current meta-wordlist set-current
meta-wordlist >order

$20001000 t, 0 t, $90 torg

include code.ft
include armv7-m-primitives.ft
include parse.ft

: constant  t: $F846 th, $0D04 th, $4800 th, $46F7 th, t, ;
: variable  t: $F846 th, $0D04 th, $4800 th, $46F7 th, tvp @ t, tcell tvp +! ;
: buffer:   t: $F846 th, $0D04 th, $4800 th, $46F7 th, tvp @ t, tvp +! ;

: ,tcall    tfind ,call ;

: immediate tlast @ tffa dup tc@ $bf and swap tc! ;
: if        s" ?branch" ,tcall there tcell tallot ;
: else      s" branch" ,tcall there tcell tallot there rot t! ;
: then      there swap t! ;
: begin     there ;
: while     s" ?branch" ,tcall there tcell tallot ;
: again     s" branch" ,tcall t, ;
: until     s" ?branch" ,tcall t, ;
: repeat    swap s" branch" ,tcall t, there swap t! ;
: postpone  s" lit" tfind ,call
            t' t,
            s" ,call" tfind ,call ;
: (         [char] ) word drop ; immediate
: \         refill drop ; immediate
: [char]    s" lit" ,tcall char t, ;

: meta:     : ;
: :         t: ,enter read-word ;
meta:       ; ,exit t; end-word ;

previous
set-current

target-wordlist >order
meta-wordlist >order

      variable base
      variable hp
      variable dp
      variable vp
      variable latest
      variable >in
      variable source#
      variable (source)
  $80 buffer:  tib
  $80 constant tib#
  $7E buffer:  psp
      variable s0
  $7E buffer:  rsp
      variable r0


include core.ft
\ include efm32pg-slstk3402a.ft
include thumbulator-m3.ft
include io.ft
include utils.ft
include compiler.ft

code rom-dump   $B500 $6831 $6030 $0008 $0001 $6830 $3604 $B402 $0001 $6830
                $3604 $B402 $2180 $B402 $BEAB $BD00 end-code

: save          latest @ $84 ! dp @ $88 ! vp $8C !
                \ 0 0 here s" saved.hex" type-hex
                0 here rom-dump
                bye ;

: slist         latest begin @ dup while
                  dup $10 dump dup link>name count type
                repeat drop ;
: cold          setup-vars abort ;

only forth

t' reset-handler 1+ $00000004 t!
t' cold             $00000080 t!
tlast @             $00000084 t!
tdp @               $00000088 t!
tvp @               $0000008C t!

.s cr

0 #target there s" hybris.hex" type-hex

only forth bye
