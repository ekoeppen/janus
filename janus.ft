\ == Janus ====================================================================

wordlist constant target-wordlist
wordlist constant meta-wordlist

: ::host::  only forth definitions ;
: ::meta::  only forth meta-wordlist >order definitions ;
: ::target::
            only forth definitions target-wordlist >order meta-wordlist >order ;

0 constant STC
1 constant DTC
2 constant ITC

  create source-file $400 allot
variable tethered
  create tether-port $400 allot
  create tether-speed $10 allot
variable threading-type

       STC threading-type !
         0 tether-port c!
s" 115200" tether-speed place
         0 source-file c!

include args.ft

parse-args

\ -- Host ---------------------------------------------------------------------

::host::

hex

include intel-hex.ft

        0 value trom
        0 value tram
        0 value tcell

        2 constant non-empty-stack
$00010000 constant target#

variable tdp
variable tvp
variable tlast 0 tlast !

create #target target# allot
       #target target# -1 fill

: pack$     dup >r 2dup c! 1+ 2dup + 0 swap ! swap cmove r> ;

: >target   #target + trom - ;
: there     tdp @ ;
: torg      tdp ! ;
: taligned  tcell 1- + tcell negate and ;
: talign    there taligned tdp ! ;
: tallot    tdp +! ;
: t!        >target tcell 4 = if l! else w! then ;
: tc@       >target c@ ;
: t@        >target l@ ;
: tc!       >target c! ;
: th!       >target w! ;
: t,        there t! tcell tallot ;
: tc,       there tc! 1 tallot ;
: th,       there th! 2 tallot ;
: talloc    trom tram <> if tvp @ t, tvp +!
            else there tcell + t, tallot
            then ;

: tdump     #target there dump ;
: (tfind)   target-wordlist search-wordlist ;
: tfind     (tfind) 0= throw >body @ ;
: t'        parse-name tfind ;
: [t']      t' postpone literal ; immediate
: mcreate   get-current target-wordlist set-current create set-current ;
: lookahead ( -- b u : parse a word, but leave it in the input stream )
            >in @ >r parse-name r> >in ! ;
: tlist     target-wordlist >order
            context @ 8 + begin @ dup while
              dup name>int >body @ 8 u.r space dup id. cr
            repeat drop
            previous ;
: final     depth if
              ." Stack not empty: " cr
              .s cr
              non-empty-stack throw
            then
            cr tlist
            ;

defer t,docon
defer t,doirq
defer t,enter
defer t,next
defer t,exit
defer t,irqexit
defer t,call
defer t,lit
defer t,branch
defer t,?branch
defer thead
defer tlink>
defer timmediate

: tfwdref    tfind t,call ;

: h:        ( -- : create a word with no name in the target dictionary )
            mcreate there , does> @ t,call ;

: t:        ( "name", -- : creates a word in the target dictionary )
            lookahead thead h: ;
: t;        ;

\ -- Meta ---------------------------------------------------------------------

::meta::

include parse.ft
include code.ft

create squote$ char s c, char " c,
create (squote)$ char ( c, char s c, char " c, char ) c,
: type$     s" type" ;

: immediate immediate timmediate ;

: postpone  parse-name target-wordlist search-wordlist dup 0= throw
            swap >body @ swap
            0< if t,lit t, s" ,call" tfwdref
               else t,call
               then ;
: if        t,?branch there tcell tallot ;
: else      t,branch there tcell tallot there rot t! ;
: then      there swap t! ;
: begin     there ;
: while     t,?branch there tcell tallot swap ;
: again     t,branch t, ;
: until     t,?branch t, ;
: repeat    t,branch t, there swap t! ;
: ahead     t,branch there tcell tallot ;
: does>     s" (does>)" tfwdref ; immediate
: for       s" >r" tfwdref there ; immediate
: next      s" (next)" tfwdref t, ; immediate
: aft       drop t,branch there tcell tallot there swap ;
: recurse   tlast @ tlink> t,call ;
: do        s" (do)" tfwdref there ;
: loop      s" (loop)" tfwdref t,?branch t, ;
: (         [char] ) word drop ;
: \         refill drop ;
: [']       t,lit t' t, ;
: [char]    t,lit char t, ;
: s"        (squote)$ 4 tfwdref
            $22 word dup dup c@ 1+ there #target + trom - swap cmove
            c@ 1+ tallot talign ;
: ."        s" type$ tfwdref ;

: constant  t: t,docon t, ;
: variable  t: t,docon tcell talloc ;
: buffer:   t: t,docon talloc ;
: i:        t: t,doirq read-word ;
: ;i        t,irqexit t; end-word ;

: meta:     : ;
: :         t: t,enter read-word ;
meta:       ; t,exit t; end-word ;

\ == Target ===================================================================

source-file count included

\ -- Host ---------------------------------------------------------------------

::host::

final

bye
