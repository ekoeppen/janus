\ == Janus ====================================================================

wordlist constant target-wordlist
wordlist constant meta-wordlist

: ::host::  only forth definitions ;
: ::meta::  only forth meta-wordlist >order definitions ;
: ::target::
            only forth definitions target-wordlist >order meta-wordlist >order ;

\ -- Host ---------------------------------------------------------------------

::host::

hex

include intel-hex.ft

        0 value trom
        0 value tram
        0 value tcell

        1 constant call-range-exceeded
        2 constant non-empty-stack
$00080000 constant target#

variable tdp
variable tvp
variable tlast 0 tlast !

create #target target# allot
       #target target# -1 fill

: pack$     dup >r 2dup c! 1+ 2dup + 0 swap ! swap cmove r> ;

: >target   #target + trom - ;
: there     tdp @ ;
: torg      tdp ! ;
: taligned  tcell 1- + tcell negate and ;
: talign    there taligned tdp ! ;
: tallot    tdp +! ;
: t!        >target tcell 4 = if l! else w! then ;
: tc@       >target c@ ;
: t@        >target l@ ;
: tc!       >target c! ;
: th!       >target w! ;
: t,        there t! tcell tallot ;
: tc,       there tc! 1 tallot ;
: th,       there th! 2 tallot ;

: tfind     target-wordlist search-wordlist 0= throw >body @ ;
: t'        parse-name tfind ;
: mcreate   get-current target-wordlist set-current create set-current ;
: lookahead ( -- b u : parse a word, but leave it in the input stream )
            >in @ >r parse-name r> >in ! ;
: tlist     target-wordlist >order
            context @ 8 + begin @ dup while
              dup name>int >body @ 8 u.r space dup id. cr
            repeat
            previous ;
: final     depth if
              ." Stack not empty: " cr
              .s cr
              non-empty-stack throw
            then
            tlist
            ;

defer ,docon
defer ,enter
defer ,exit
defer ,call
defer thead
defer tlink>
defer timmediate

: ,tcall    tfind ,call ;

: h:        ( -- : create a word with no name in the target dictionary )
            mcreate there , does> @ ,call ;

: t:        ( "name", -- : creates a word in the target dictionary )
            lookahead thead h: ;
: t;        ;

\ -- Meta ---------------------------------------------------------------------

::meta::

include parse.ft
include code.ft

create squote$ char s c, char " c,
create (squote)$ char ( c, char s c, char " c, char ) c,
: type$     s" type";

: immediate immediate timmediate ;

: postpone  parse-name target-wordlist search-wordlist dup 0= throw
            swap >body @ swap
            0< if s" lit" ,tcall t, s" ,call" ,tcall
               else ,call
               then ;
: if        s" ?branch" ,tcall there tcell tallot ;
: else      s" branch" ,tcall there tcell tallot there rot t! ;
: then      there swap t! ;
: begin     there ;
: while     s" ?branch" ,tcall there tcell tallot swap ;
: again     s" branch" ,tcall t, ;
: until     s" ?branch" ,tcall t, ;
: repeat    s" branch" ,tcall t, there swap t! ;
: recurse   tlast @ tlink> ,call ;
: do        s" (do)" ,tcall there ;
: loop      s" (loop)" ,tcall s" ?branch" ,tcall t, ;
: (         [char] ) word drop ;
: \         refill drop ;
: [']       s" lit" ,tcall t' t, ;
: [char]    s" lit" ,tcall char t, ;
: s"        (squote)$ 4 ,tcall
            $22 word dup dup c@ 1+ there #target + trom - swap cmove
            c@ 1+ tallot talign ;
: ."        s" type$ ,tcall ;

: constant  t: ,docon t, ;
: variable  t: ,docon tvp @ t, tcell tvp +! ;
: buffer:   t: ,docon tvp @ t, tvp +! ;

: meta:     : ;
: :         t: ,enter read-word ;
meta:       ; ,exit t; end-word ;

\ == Target ===================================================================

\ include CoreForth-0/cortex-stc/boards/thumbulator-m3.ft
\ include CoreForth-0/cortex-stc/boards/nucleo-f103rb.ft
include CoreForth-0/msp430/boards/msp-exp430g2.ft

\ -- Generic ------------------------------------------------------------------

::host::

: tlist     target-wordlist >order
            context @ 8 + begin @ dup while
              dup name>int >body @ 8 u.r space dup id. cr
            repeat
            previous ;
: final     depth if
              ." Stack not empty: " cr
              .s cr
              non-empty-stack throw
            then
            ." Words" cr tlist ;

final

bye
